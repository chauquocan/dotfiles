{ config, pkgs, lib, home-manager, ... }:

let
  user = "quocan";
  # Define the content of your file as a derivation
  myEmacsLauncher = pkgs.writeScript "emacs-launcher.command" ''
    #!/bin/sh
    emacsclient -c -n &
  '';
  sharedFiles = import ../shared/files.nix { inherit config pkgs; };
  additionalFiles = import ./files.nix { inherit user config pkgs; };
in
{
  imports = [
   ./dock
  ];

  # It me
  users.users.${user} = {
    name = "${user}";
    home = "/Users/${user}";
    isHidden = false;
    shell = pkgs.zsh;
  };

  homebrew = {
    enable = true;
    onActivation = {
        autoUpdate = false;
        upgrade = false;
        cleanup = "zap";
    };
    casks = pkgs.callPackage ./casks.nix {};

    brews = [
      "mysql"
      "fvm"
      "php@7.4"
      "openjdk@17"
      "composer"
    ];

    # These app IDs are from using the mas CLI app
    # mas = mac app store
    # https://github.com/mas-cli/mas
    #
    # $ nix shell nixpkgs#mas
    # $ mas search <app name>
    #
    # If you have previously added these apps to your Mac App Store profile (but not installed them on this system),
    # you may receive an error message "Redownload Unavailable with This Apple ID".
    # This message is safe to ignore. (https://github.com/dustinlyons/nixos-config/issues/83)

    masApps = {
      "Xcode" = 497799835;
      "EZVIZ for Computer" = 1594552558;
    };
  };

  # Enable home-manager
  home-manager = {
    useGlobalPkgs = true;
    users.${user} = { pkgs, config, lib, ... }:{
      home = {
        enableNixpkgsReleaseCheck = false;
        packages = pkgs.callPackage ./packages.nix {};
        file = lib.mkMerge [
          sharedFiles
          additionalFiles
          { "emacs-launcher.command".source = myEmacsLauncher; }
        ];

        stateVersion = "23.11";
      };
      programs = {} // import ../shared/home-manager.nix { inherit config pkgs lib; };

      # Marked broken Oct 20, 2022 check later to remove this
      # https://github.com/nix-community/home-manager/issues/3344
      manual.manpages.enable = false;
    };
  };

  # Fully declarative dock using the latest from Nix Store
  local = { 
    dock = {
      enable = true;
      entries = [
        { path = "${pkgs.iterm2}/Applications/Iterm2.app/"; }
        { path = "/System/Applications/Notes.app/"; }
        { path = "/Applications/Arc.app/"; }
        { path = "/Applications/Visual Studio Code.app/"; }
        { path = "${pkgs.jetbrains.phpstorm}/Applications/PhpStorm.app/"; }
        { path = "${pkgs.jetbrains.datagrip}/Applications/DataGrip.app/"; }
        { path = "/Applications/Postman.app/"; }
        { path = "/Applications/Microsoft Teams.app/"; }
        { path = "/System/Applications/Music.app/"; }
        { path = "/System/Applications/System Settings.app/"; }
        {
          path = "${config.users.users.${user}.home}/Downloads";
          section = "others";
          options = "--sort name --view grid --display stack";
        }
      ];
    };
  };
}
